% Input variables.
metavar ilabels, a, b, c ::=
metavar paths , h {{tex \pi}} ::=
metavar gindex ,  n , x , y, z , w , m , o ::=  % Indexes into the graph.

indexvar ivar, i , k , j , l ::= 

grammar

% Intuitionistic Logic:
X, Y, Z :: 'Iform_' ::=
  | 1    ::   :: 1  
  | X x Y ::   :: Conj
  {{tex [[X]] \times [[Y]] }}
  | X -> Y ::   :: Imp
  {{tex [[X]] \to [[Y]] }}
  | Gf A     ::   :: G
  {{tex \mathsf{G}\,[[A]] }}
  | ( X )   :: M :: Parens 
  | h( X )  :: M :: HiddenParens 
  {{ tex [[X]] }}

% Co-intuitionistic Logic:
R, S, T :: 'Cform_' ::=
  | 0   ::   :: 0
  | S + T ::   :: Disj
  {{tex [[S]] + [[T]] }}
  | S - T  ::   :: Subtrac
  {{tex [[S]] - [[T]] }}
  | H A     ::   :: H
  {{tex \mathsf{H}\,[[A]] }}
  | ( T )   :: M :: Parens 
  | h( T )  :: M :: HiddenParens 
  {{ tex [[T]] }}

% Bi-intuitionistic (Linear) Logic:
A, B, C :: 'Bform_' ::=
  | True    ::   :: 1
  {{tex \top }}
  | False   ::   :: 0
  {{tex \perp }}
  | A x B ::   :: Conj
  {{tex [[A]] \times [[B]] }}
  | A (x) B ::   :: Tensor
  {{tex [[A]] \otimes [[B]] }}  
  | A + B ::   :: Disj
  {{tex [[A]] + [[B]] }}
  | A (+) B ::   :: Par
  {{tex [[A]] \oplus [[B]] }}    
  | A -> B ::   :: Imp
  {{tex [[A]] \to [[B]] }}
  | A -o B ::   :: LImp
  {{tex [[A]] \limp [[B]] }}  
  | A - B  ::   :: Subtrac
  {{tex [[A]] - [[B]] }}
  | A *- B ::   :: LSubtrac
  {{tex [[A]] \colimp [[B]] }}
  | F X     ::   :: F
  {{tex \mathsf{F}\,[[X]] }}
  | J S     ::   :: J
  {{tex \mathsf{J}\,[[S]] }}  
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}

Gr {{tex G }} :: 'E_' ::=
  | .             ::    :: Empty
  {{tex \cdot }}
  | { ( x , y ) } ::   :: El
  | x <= y     ::   :: El2
  {{tex [[x]] \leq [[y]] }}
  | Gr , Gr'      ::   :: Ext
  | Gr U Gr'      ::   :: Union
  {{tex [[Gr]] \cup [[Gr']] }}
  | ( Gr )        ::   :: Paren
  | h( Gr )       :: M :: HiddenParens 
  {{ tex [[Gr]] }}

% Intuitionistic Contexts:
H {{tex \Theta}}, P {{tex \Phi}} :: 'IG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | X @ w       ::   :: El
  | H , P      ::   :: Ext
  | ( H )       :: S :: Paren
  | h( H )      :: M :: HiddenParens 
  {{ tex [[H]] }}

% Co-intuitionistic Contexts:
I {{tex \Psi}}, PI {{tex \Pi}} :: 'CG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | T @ w       ::   :: El
  | I , PI      ::   :: Ext
  | ( I )       :: S :: Paren
  | h( I )      :: M :: HiddenParens 
  {{ tex [[I]] }}   

% Bi-intuitionistic Contexts:
G {{tex \Gamma}}, D {{tex \Delta}} :: 'BG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | A @ w       ::   :: El
  | G , G'      ::   :: Ext
  | ( G )       :: S :: Paren
  | h( G )      :: M :: HiddenParens 
  {{ tex [[G]] }}   
  
terminals :: '' ::=
  | F    ::  :: FunctorF
  {{tex \mathsf{F} }}
  | Gf    ::  :: FunctorG
  {{tex \mathsf{G} }}
  | Hf    ::  :: FunctorH
  {{tex \mathsf{H} }}
  | J    ::  :: FunctorJ
  {{tex \mathsf{J} }}  
  | 1    ::  :: One
  | 0    ::  :: Zero
  | []   :: :: Box  {{ tex \Box }}
  | |=   :: :: models  {{ tex \models }}
  | |-   :: :: Turnstyle  {{tex \vdash }}
  | star :: :: star   {{ tex \mathop{\star} }}
  | >>   :: :: RR   {{ tex \gg}}
  | _    :: :: Under   {{tex \Box}}
  | \    :: :: slash  {{ tex \lambda }}
  | ~>   :: :: step  {{ tex \redto }}
  | ~*>   :: :: sstep  {{ tex \redto^* }}
  | >=   :: :: succeq {{ tex \succeq }}
  | <=   :: :: preceq {{ tex \preceq }}
  | ->   :: :: rightarrow  {{ tex \rightarrow }}
  | <-   :: :: leftarrow  {{ tex \leftarrow }}
  | -<   :: :: Yleft  {{ tex \Yleft }}
  | ~*>  :: :: mstep  {{ tex \redto^* }}
  | =    :: :: Eq
  | ==   :: :: Equiv  {{ tex \equiv }}
  | =~   :: :: Cong  {{ tex \cong }}
  | bot  :: :: Bottom   {{ tex \mathop{\perp} }}
  | top  :: :: Top   {{ tex \top }}
  | *    :: :: Cut  {{ tex \bullet}}
  | ~    :: :: Tilde  {{tex \mathop{\sim} }}
  | \    :: :: Lam {{ tex \lambda }}
  | mu   :: :: Mu {{ tex \mu }}
  | !=   :: :: Neq {{tex \neq }}
  | FV   :: :: FV  {{ tex \mathsf{FV} }}
  | in   :: :: In {{ tex \in }}


 formula :: 'formula_' ::=
  | judgement               ::  :: judgement
  | formula <= formula'     :: M :: PJ
  | ( formula )             :: M :: ParenF
  | formula && formula'     ::  :: judgement_list 
  {{ tex [[formula]] \qquad [[formula']] }}
  | ( formula )             ::  :: parens
  | x Gr y                  ::  :: GraphRel
  {{tex [[x]] [[Gr]] [[y]] }}
  | x notin Gr , G , D      ::  :: wnotin
  {{tex [[x]] \not\in |[[Gr]]|,|[[G]]|,|[[D]]| }}
  | x notin Gr , H , G , D , I      ::  :: wnotin4
  {{tex [[x]] \not\in |[[Gr]]|,|[[H]]|,|[[G]]|,|[[D]]|,|[[I]]| }}
  | x notin Gr , H      ::  :: wnotin2
  {{tex [[x]] \not\in |[[Gr]]|,|[[H]]| }}
  | x notin Gr , I      ::  :: wnotin3
  {{tex [[x]] \not\in |[[Gr]]|,|[[I]]| }}    
  | n in Nodes ( Gr )       :: M :: Nodes1
  {{tex [[n]] \in \mathsf{Nodes}([[Gr]]) }}
  | n in Nodes ( G )        :: M :: Nodes2
  {{tex [[n]] \in \mathsf{Nodes}([[G]]) }}  

defns
  Jtyp :: '' ::=

% Intuitionistic Logic:
defn
 Gr ; H |-I X @ w :: :: ILog :: 'I_' 
 {{tex [[Gr]];[[H]] \vdash_{\mathsf{I} } [[X]]@[[w]] }}
 by

 Gr, w1 <= w1;H |-I Y @ w2
 ------------------------ :: rl
 Gr;H |-I Y@w2

 w1 Gr w2 && w2 Gr w3
 Gr,w1 <= w3;H |-I Y@w
 ---------------------- :: ts
 Gr;H |-I Y@w

 ---------------- :: id
 Gr;Y@w |-I Y@w

 Gr;H |-I Y@w1
 ----------------------- :: wk
 Gr;H, X@w2 |-I Y@w1

 Gr;H, X@w2, X@w2 |-I Y@w1
 ------------------------------- :: cr
 Gr;H, X@w2 |-I Y@w1

 Gr;H1,X@w1, Y@w2,H2 |-I Z@w
 --------------------------------- :: ex
 Gr;H1,Y@w2, X@w1,H2 |-I Z@w

 w1 Gr w2
 Gr;H , X@w1 , X@w2 |-I Y@w
 -------------------------------- :: mL
 Gr;H , X@w1 |-I Y@w

 w2 Gr w1
 Gr;H |-I Y@w2
 --------------- :: mR
 Gr;H |-I Y@w1

 Gr;H |-I Y@w1
 --------------------------- :: tL
 Gr;H , 1@w2 |-I Y@w1

 ----------------- :: tR
 Gr;H |-I 1@w

 Gr;H , X@w1, Y@w1 |-I Z@w2
 -------------------------------- :: pL
 Gr;H , (X x Y) @w1   |-I Z@w2

 Gr;H1 |-I X@w && Gr;H2 |-I Y@w
 ---------------------------------- :: pR
 Gr;H1,H2 |-I (X x Y)@w

 w1 Gr w2
 Gr;H2 |-I X@w2 && Gr;H1, Y@w2 |-I Z@w
 ------------------------------------------- :: IL
 Gr;H1 , H2, (X -> Y)@w1 |-I Z@w 

 w2 notin Gr, H
 Gr, w1 <= w2;H, X@w2 |-I Y@w2
 --------------------------------- :: IR
 Gr;H |-I (X -> Y)@w1

 Gr;H2 |-I X@w2 && Gr;H1,X@w2 |-I Z@w1
 -------------------------------------------- :: cut
 Gr;H1,H2 |-I Z@w1

 Gr;H | . |-L A@w | .
 --------------------- :: gR
 Gr;H |-I Gf A @ w

% Co-intuitionistic Logic:
defn
 Gr ; S @ w |-C I :: :: CLog :: 'C_' 
 {{tex [[Gr]];[[S]]@[[w]] \vdash_{\mathsf{C} } [[I]] }}
 by

 Gr, w1 <= w1;S @ w2 |-C I
 ------------------------- :: rl
 Gr;S @ w2 |-C I

 w1 Gr w2 && w2 Gr w3
 Gr,w1 <= w3;S @ w |-C I
 ----------------------- :: ts
 Gr;S @ w |-C I

 ------------------ :: id
 Gr;S @ w |-C S @ w

 Gr;S @ w1 |-C I
 ---------------------- :: wk
 Gr;S @ w1 |-C T @ w2,I

 Gr;S @ w1 |-C T @ w2, T @ w2, I
 ------------------------------- :: cr
 Gr;S @ w1 |-C T @ w2, I

 Gr;R @ w |-C I1,S @ w1, T @ w2, I2
 ---------------------------------- :: ex
 Gr;R @ w |-C I1,T @ w2, S @ w1, I2

 w1 Gr w2
 Gr;S @ w2 |-C I
 --------------------------- :: mL
 Gr;S @ w1 |-C I

 w2 Gr w1
 Gr;S @ w |-C T @ w2 , T @ w1, I
 ------------------------------- :: mR
 Gr;S @ w |-C T @ w1, I

 ------------------ :: fL
 Gr;0@w |-C I

 Gr;S @ w1 |-C I
 --------------------------- :: fR
 Gr;S @ w1 |-C 0 @ w2, I

 Gr;S @ w |-C I1 && Gr;T @ w |-C I2
 ---------------------------------- :: dL
 Gr;S + T @ w |-C I1,I2

 Gr;R @ w1 |-C S @ w2, T @ w2, I
 ------------------------------- :: dR
 Gr;R @ w1 |-C S + T @ w2, I

 w2 notin Gr, I
 Gr,w2 <= w1;S @ w2 |-C T @ w2, I
 ------------------------------- :: sL
 Gr;S - T @ w1 |-C I

 w2 Gr w1
 Gr;R @ w |-C S @ w2 , I2 && Gr;T @ w2 |-C I1
 -------------------------------------------- :: sR
 Gr;R @ w |-C S - T @ w1, I1,I2

 Gr;S @ w1 |-C T @ w2, I2 && Gr;T @ w2 |-C I1
 --------------------------------------------- :: cut
 Gr;S @ w1 |-C I1,I2

 Gr;. | A @ w |-L . | I
 ----------------------- :: hL
 Gr;H A @ w |-C I

% Bi-intuitionistic Linear Logic:
defn
 Gr ; H | G |-L D | I :: :: LBILog :: 'L_' 
 {{tex [[Gr]];[[H]] \mid [[G]] \vdash_{\mathsf{L} } [[D]] \mid [[I]] }}
 by

 % Graphs:

 Gr, w <= w;H | G |-L D | I
 -------------------------- :: rl
 Gr;H | G |-L D | I

 w1 Gr w2 && w2 Gr w3
 Gr,w1 <= w3;H | G |-L D | I
 --------------------------- :: ts
 Gr;H | G |-L D | I

 % Axiom:

 --------------------------- :: id
 Gr;. | A @ w |-L A @ w | .

 % Structural Rules:

 Gr; H | G |-L D | I
 --------------------------- :: wkL
 Gr; H, X @ w | G |-L D | I

 Gr; H | G |-L D | I
 --------------------------- :: wkR
 Gr; H | G |-L D | S @ w, I

 Gr;H, X @ w, X @ w | G |-L D | I
 --------------------------------- :: ctrL
 Gr;H, X @ w | G |-L D | I

 Gr;H | G |-L D | S @ w, S @ w , I
 ---------------------------------- :: ctrR
 Gr;H | G |-L D | S @ w , I

 Gr;H | G1,A @ w1,B @ w2,G2 |-L D | I
 -------------------------------------- :: exL
 Gr;H | G1,B @ w2,A @ w1,G2 |-L D | I

 Gr;H1,X @ w1, Y @ w2,H2 | G |-L D | I
 -------------------------------------- :: IexL
 Gr;H1,Y @ w2, X @ w1,H2 | G |-L D | I

 Gr;H | G |-L D1,A @ w1, B @ w2,D2 | I
 -------------------------------------- :: exR
 Gr;H | G |-L D1,B @ w2, A @ w1,D2 | I

 Gr;H | G |-L D | I1,S @ w1, T @ w2,I2
 -------------------------------------- :: CexL
 Gr;H | G |-L D | I1,T @ w2, S @ w1,I2

 % Monotonicity Rules:

 w1 Gr w2
 Gr;H | G , A @ w1 , A @ w2 |-L D | I
 ------------------------------------- :: mL
 Gr;H | G , A @ w1 |-L D | I

 w2 Gr w1
 Gr;H | G |-L A @ w2 , A @ w1, D | I
 ------------------------------------ :: mR
 Gr;H | G |-L A @ w1, D | I

 w1 Gr w2
 Gr;H , X @ w1 , X @ w2 | G |-L D | I
 ------------------------------------- :: ImL
 Gr;H , X @ w1 | G |-L D | I

 w2 Gr w1
 Gr;H | G |-L D | T @ w2 , T @ w1, I
 ------------------------------------ :: CmR
 Gr;H | G |-L D | T @ w1, I

 % Cut:

 Gr; H1 | G1 |-L A @ w, D2 | I1 && Gr; H2 | A @ w,G2 |-L D1 | I2
 ----------------------------------------------------------------- :: cut
 Gr;H1,H2 | G1,G2 |-L D1,D2 | I1,I2

 Gr;H2 |-I X @ w && Gr;H1, X @ w | G |-L D | I
 ---------------------------------------------- :: Icut
 Gr;H1,H2 | G |-L D | I

 Gr;H | G |-L D | I1, S @ w && Gr;S @ w |-C I2
 ---------------------------------------------- :: Ccut
 Gr;H | G |-L D | I1, I2

 % Units:

 Gr;H | G |-L D | I
 --------------------------- :: IL
 Gr;H | G, True @ w |-L D | I

 ------------------------ :: IR
 Gr;. | . |-L True @ w | .

 ----------------------- :: flL
 Gr;. | False @ w |-L . | .

 Gr;H | G |-L D | I
 ------------------------- :: flR
 Gr;H | G |-L False @ w,D | I

 % Additives from IL and coIL:
 
 Gr;H1, X @ w, Y @ w,H2 | G |-L D | I
 ------------------------------------- :: cL
 Gr;H1, X x Y@w,H2 | G |-L D | I

 Gr;H | G |-L D | I1, S @ w, T @ w, I2
 -------------------------------------- :: dR
 Gr;H | G |-L D | I1, S + T@w, I2

 % Tensor:

 Gr; H | G1, A @ w, B @ w, G2 |-L D | I
 --------------------------------------- :: tL
 Gr; H | G1, A (x) B @w, G2 |-L D | I

 Gr; H1 | G1 |-L A @ w,D1 | I1 && Gr; H2 | G2 |-L B @ w,D2 | I2
 ---------------------------------------------------------------- :: tR
 Gr; H1,H2 | G1,G2 |-L A (x) B@w,D1,D2 | I1,I2

 % Par:

 Gr;H1 | G1, A @ w |-L D1 | I1 && Gr;H2 | G2, B @ w |-L D2 | I2
 ---------------------------------------------------------------- :: pL
 Gr;H1,H2 | G1, G2, A (+) B @ w |-L D1,D2 | I1,I2

 Gr;H | G |-L D1, A @ w, B @ w, D2 | I
 -------------------------------------- :: pR
 Gr;H | G |-L D1,  A (+) B @w, D2 | I

 % Implication:

 w2 notin Gr,H,G,D,I
 Gr, w1 <= w2; H | G, A @ w2 |-L B @ w2,D | I
 --------------------------------------------- :: ImpR
 Gr; H | G |-L A -o B @ w1,D | I

 w1 Gr w2
 Gr;H1 | G1 |-L A @ w2,D1 | I1 && Gr;H2 | G2,B @ w2 |-L D2 | I2
 ---------------------------------------------------------------- :: ImpL
 Gr;H1,H2 | G1,G2,A -o B @ w1 |-L D1,D2 | I1,I2

 w1 Gr w2
 Gr;H1 |-I X @ w2 && Gr;H2,Y @ w2 | G |-L D | I
 ----------------------------------------------- :: IImpL
 Gr;H1,H2, X -> Y@w1 | G |-L D | I

 % Co-implication:

 w2 notin Gr,H,G,D,I
 Gr, w2 <= w1;H | G,A @ w2 |-L B @ w2, D | I
 ------------------------------------------- :: sL
 Gr;H | G,A *- B @ w1 |-L D | I

 w2 Gr w1
 Gr;H1 | G1 |-L A @ w2,D1 | I1 && Gr;H2 | G2,B @ w2 |-L D2 | I2
 ---------------------------------------------------------------- :: sR
 Gr;H1,H2 | G1,G2 |-L A *- B @ w1,D1,D2 | I1,I2

 w2 Gr w1
 Gr;H | G |-L D | S @ w2,I1 && Gr;T @ w2 |-C I2
 ---------------------------------------------------------------- :: CsR
 Gr;H | G |-L D | S - T @ w1,I1,I2

 % Intuitionistic Functors:

 Gr;H, X @ w | G |-L D | I
 --------------------------- :: fL
 Gr;H | G,F X @ w |-L D | I 

 Gr; H |-I X @ w
 -------------------------- :: fR
 Gr; H | . |-L F X @ w | .

 Gr;H | G, A @ w |-L D | I
 ----------------------------- :: gL
 Gr;H, Gf A @ w | G |-L D | I 

 % Co-intuitionistic Functors:

 Gr; S @ w |-C I
 ------------------------- :: jL
 Gr;. | J S @ w |-L . | I

 Gr;H | G |-L D | S @ w,I
 --------------------------- :: jR
 Gr;H | G |-L D,J S@w | I

 Gr;H | G |-L D,A @ w | I
 --------------------------- :: hR
 Gr;H | G |-L D | H A@w,I

% Bi-intuitionistic Logic:
defn
 Gr ; G |-L D :: :: BILog :: '' 
 {{tex [[Gr]];[[G]] \vdash_{\mathsf{L} } [[D]] }}
 by

 % Graphs:

 Gr, w <= w; G |-L D
 ------------------ :: rl
 Gr; G |-L D

 w1 Gr w2 && w2 Gr w3
 Gr,w1 <= w3;G |-L D
 -------------------- :: ts
 Gr;G |-L D

 % Axiom:

 ------------------ :: id
 Gr;A @ w |-L A @ w

 % Structural Rules:

 Gr; G |-L D 
 ------------------ :: wkL
 Gr; G, A @ w |-L D

 Gr; G |-L D
 ------------------ :: wkR
 Gr; G |-L A @ w, D

 Gr;G, A @ w, A @ w |-L D
 ------------------------ :: ctrL
 Gr;G, A @ w |-L D

 Gr; G |-L A @ w, A @ w , D
 -------------------------- :: ctrR
 Gr; G |-L A @ w , D

 Gr;G1,A @ w1,B @ w2,G2 |-L D
 ---------------------------- :: exL
 Gr;G1,B @ w2,A @ w1,G2 |-L D

 Gr;G |-L D1,A @ w1, B @ w2,D2
 ----------------------------- :: exR
 Gr;G |-L D1,B @ w2, A @ w1,D2

 % Monotonicity Rules:

 w1 Gr w2
 Gr;G , A @ w1 , A @ w2 |-L D
 --------------------------- :: mL
 Gr;G , A @ w1 |-L D

 w2 Gr w1
 Gr;G |-L A @ w2 , A @ w1, D
 --------------------------- :: mR
 Gr;G |-L A @ w1, D

 % Cut:

 Gr; G1 |-L A @ w, D2 && Gr; A @ w,G2 |-L D1
 ------------------------------------------- :: cut
 Gr; G1,G2 |-L D1,D2

 % Units:

 Gr;G |-L D
 ----------------- :: IL
 Gr;G, True @ w |-L D

 -------------- :: IR
 Gr;. |-L False @ w

 -------------- :: flL
 Gr;True @ w |-L .

 Gr;G |-L D
 ---------------- :: flR
 Gr;G |-L False @ w,D

 Gr; G1, A @ w, B @ w, G2 |-L D 
 ------------------------------ :: cL
 Gr; G1, A x B @ w, G2 |-L D

 Gr; G1 |-L A @ w,D1 && Gr; G2 |-L B @ w,D2
 ------------------------------------------ :: cR
 Gr; G1,G2 |-L A x B @ w

 % Par:

 Gr;G1, A @ w |-L D1 && Gr;G2, B @ w |-L D2
 ------------------------------------------ :: dL
 Gr;G1, G2, A + B @ w |-L D1,D2

 Gr;G |-L D1, A @ w, B @ w, D2
 ----------------------------- :: dR
 Gr;G |-L D1, A + B @ w, D2

 % Implication:

 w2 notin Gr,G,D
 Gr, w1 <= w2; G, A @ w2 |-L B @ w2,D
 ------------------------------------ :: ImpR
 Gr; G |-L A -> B @ w1,D

 w1 Gr w2
 Gr;G1 |-L A @ w2,D1 && Gr;G2,B @ w2 |-L D2
 ------------------------------------------ :: ImpL
 Gr;G1,G2,A -> B @ w1 |-L D1,D2

 % Co-implication:

 w2 notin Gr,G,D
 Gr, w2 <= w1; G,A @ w2 |-L B @ w2, D
 ----------------------------------- :: sL
 Gr;G,A - B @ w1 |-L D

 w2 Gr w1
 Gr;G1 |-L A @ w2,D1 && Gr;G2,B @ w2 |-L D2
 ------------------------------------------ :: sR
 Gr;G1,G2 |-L A - B @ w1,D1,D2