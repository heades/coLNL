% Input variables.
metavar vars ,  n , x {{tex \alpha}} , y, z , w , m , o ::=  
indexvar ivar, i , k , j , l ::= 

grammar

% Cointuitionistic Logic:
R, S, T :: 'Cform_' ::=
  | 0   ::   :: 0
  | S + T ::   :: Disj
  {{tex [[S]] + [[T]] }}
  | S - T  ::   :: Subtrac
  {{tex [[S]] - [[T]] }}
  | H A     ::   :: H
  {{tex \mathsf{H}\,[[A]] }}
  | ( T )   :: M :: Parens 
  | h( T )  :: M :: HiddenParens 
  {{ tex [[T]] }}

% Cointuitionistic (Linear) Logic:
A, B, C :: 'Bform_' ::=
  | False   ::   :: 0
  {{tex \perp }}
  | A + B ::   :: Disj
  {{tex [[A]] + [[B]] }}
  | A (+) B ::   :: Par
  {{tex [[A]] \oplus [[B]] }}    
  | A - B  ::   :: Subtrac
  {{tex [[A]] - [[B]] }}
  | A *- B ::   :: LSubtrac
  {{tex [[A]] \colimp [[B]] }}
  | J S     ::   :: J
  {{tex \mathsf{J}\,[[S]] }}  
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}

% Terms:
term, t :: 'Term_' ::=
  | x :: :: Var  
  | < t1 , t2 > :: :: CoApp
    {{tex \langle [[t1]] , [[t2]] \rangle }}  

% Cointuitionistic Contexts:
I {{tex \Psi}}, PI {{tex \Pi}} :: 'CG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | T           ::   :: El
  | I , PI      ::   :: Ext
  | ( I )       :: S :: Paren
  | h( I )      :: M :: HiddenParens 
  {{ tex [[I]] }}   

% Cointuitionistic Contexts:
G {{tex \Gamma}}, D {{tex \Delta}} :: 'BG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | A           ::   :: El
  | G , G'      ::   :: Ext
  | ( G )       :: S :: Paren
  | h( G )      :: M :: HiddenParens 
  {{ tex [[G]] }}   
  
terminals :: '' ::=
  | F    ::  :: FunctorF
  {{tex \mathsf{F} }}
  | Gf    ::  :: FunctorG
  {{tex \mathsf{G} }}
  | Hf    ::  :: FunctorH
  {{tex \mathsf{H} }}
  | J    ::  :: FunctorJ
  {{tex \mathsf{J} }}  
  | 1    ::  :: One
  | 0    ::  :: Zero
  | []   :: :: Box  {{ tex \Box }}
  | |=   :: :: models  {{ tex \models }}
  | |-   :: :: Turnstyle  {{tex \vdash }}
  | star :: :: star   {{ tex \mathop{\star} }}
  | >>   :: :: RR   {{ tex \gg}}
  | _    :: :: Under   {{tex \Box}}
  | \    :: :: slash  {{ tex \lambda }}
  | ~>   :: :: step  {{ tex \redto }}
  | ~*>   :: :: sstep  {{ tex \redto^* }}
  | >=   :: :: succeq {{ tex \succeq }}
  | <=   :: :: preceq {{ tex \preceq }}
  | ->   :: :: rightarrow  {{ tex \rightarrow }}
  | *-   :: :: colimp  {{ tex \colimp }}  
  | <-   :: :: leftarrow  {{ tex \leftarrow }}
  | -<   :: :: Yleft  {{ tex \Yleft }}
  | ~*>  :: :: mstep  {{ tex \redto^* }}
  | =    :: :: Eq
  | ==   :: :: Equiv  {{ tex \equiv }}
  | =~   :: :: Cong  {{ tex \cong }}
  | bot  :: :: Bottom   {{ tex \mathop{\perp} }}
  | top  :: :: Top   {{ tex \top }}
  | *    :: :: Cut  {{ tex \bullet}}
  | ~    :: :: Tilde  {{tex \mathop{\sim} }}
  | \    :: :: Lam {{ tex \lambda }}
  | mu   :: :: Mu {{ tex \mu }}
  | !=   :: :: Neq {{tex \neq }}
  | FV   :: :: FV  {{ tex \mathsf{FV} }}
  | in   :: :: In {{ tex \in }}


 formula :: 'formula_' ::=
  | judgement               ::  :: judgement
  | formula <= formula'     :: M :: PJ
  | ( formula )             :: M :: ParenF
  | formula && formula'     ::  :: judgement_list 
  {{ tex [[formula]] \qquad [[formula']] }}
  | ( formula )             ::  :: parens

defns
  Jtyp :: '' ::=

% Co-intuitionistic Logic:
defn
 S |-C I :: :: CLog :: 'C_' 
 {{tex [[S]] \vdash_{\mathsf{C} } [[I]] }}
 by

 ------- :: id
 S |-C S

 S |-C I1,I2
 ------------- :: wk
 S |-C I1,T,I2

 S |-C I1,T,T, I2
 ---------------- :: cr
 S |-C I1,T,I2

 R |-C I1,S, T, I2
 ----------------- :: ex
 R |-C I1,T, S, I2

 ------- :: fL
 0 |-C I

 S |-C I1,I2
 ------------- :: fR
 S |-C I1,0,I2

 S |-C I1 && T |-C I2
 -------------------- :: dL
 S + T |-C I1,I2

 R |-C I1,S, T, I2
 ------------------ :: dR
 R |-C I1,S + T, I2

 S |-C T, I
 ----------- :: sL
 S - T |-C I

 R |-C I1, S,I2 && T |-C I3
 -------------------------- :: sR
 R |-C I1,S - T, I2,I3

 S |-C T, I2 && T |-C I1
 ----------------------- :: cut
 S |-C I1,I2

 A |-L . | I
 ----------- :: hL
 H A |-C I

% Cointuitionistic Linear Logic:
defn
 A |-L D | I :: :: LBILog :: 'L_' 
 {{tex [[A]] \vdash_{\mathsf{L} } [[D]] \mid [[I]] }}
 by

 % Axiom:

 ----------- :: id
 A |-L A | .

 % Structural Rules:

 A |-L D | I1,I2
 ------------------ :: wk
 A |-L D | I1,S,I2

 A |-L D | I1,S,S,I2
 ------------------- :: ctr
 A |-L D | I1,S,I2

 A |-L D1,A, B,D2 | I
 -------------------- :: ex
 A |-L D1,B, A,D2 | I

 A |-L D | I1,S, T,I2
 -------------------- :: Cex
 A |-L D | I1,T, S,I2

 % Cut:

 A |-L D1,B,D3 | I1 &&   B |-L D2 | I2
 ------------------------------------- :: cut
 A |-L D1,D2,D3 | I1,I2

 A |-L D | I1,S,I3 && S |-C I2
 ----------------------------- :: Ccut
 A |-L D | I1,I2,I3

 % Units:

 --------------- :: flL
 False |-L . | .

 A |-L D1,D2 | I
 ---------------------- :: flR
 A |-L D1,False,D2 | I

 % Additives from coIL:
 
 A |-L D | I1, S, T, I2
 ----------------------- :: dR
 A |-L D | I1, S + T, I2

 % Cotensor:

 A |-L D1 | I1 && B |-L D2 | I2
 ------------------------------ :: pL
 A (+) B |-L D1,D2 | I1,I2

 A |-L D1, B, C, D2 | I
 ------------------------- :: pR
 A |-L D1, B (+) C, D2 | I

 % Co-implication:
 
 A |-L B, D | I
 ---------------- :: sL
 A *- B |-L D | I

 A |-L D1,B,D2 | I1 && C |-L D3 | I2
 ----------------------------------- :: sR
 A |-L D1,B *- C,D2,D3 | I1,I2

 A |-L D | I1,S,I2 && T |-C I3
 ----------------------------- :: CsR
 A |-L D | I1,S - T,I2,I3

 % Co-intuitionistic Functors:

 S |-C I
 ------------- :: jL
 J S |-L . | I

 A |-L D | S,I
 --------------- :: jR
 A |-L D,J S | I

 A |-L D,B | I
 ---------------- :: hR
 A |-L D | H B,I